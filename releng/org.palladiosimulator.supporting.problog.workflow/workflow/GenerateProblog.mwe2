module org.palladiosimulator.supporting.problog.GenerateProblog

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*
import tools.mdsd.ecoreworkflow.mwe2lib.bean.EclipseRCPSupportingStandaloneSetup

var workspaceRoot = "../.."
var rootPath = "${workspaceRoot}/bundles"

Workflow {
	
	bean = EclipseRCPSupportingStandaloneSetup {
		scanClassPath = true
		platformUri = workspaceRoot
		
		uriMap = {
			from = "platform:/plugin/org.palladiosimulator.supporting.prolog.model/model/prolog.genmodel"
			to = "platform:/resource/org.palladiosimulator.supporting.prolog.model/model/prolog.genmodel"
		}

		registerGenModelFile = "platform:/resource/org.palladiosimulator.supporting.prolog.model/model/prolog.genmodel"
		
		uriMap = {
			from = "platform:/plugin/org.palladiosimulator.supporting.problog.model/model/problog.genmodel"
			to = "platform:/resource/org.palladiosimulator.supporting.problog.model/model/problog.genmodel"
		}

		registerGenModelFile = "platform:/resource/org.palladiosimulator.supporting.problog.model/model/problog.genmodel"
	}
	
	component = XtextGenerator {
		
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.palladiosimulator.supporting.problog"
				rootPath = rootPath
				runtimeTest = {
					root = "${workspaceRoot}/tests/org.palladiosimulator.supporting.problog.tests"
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					root = "${workspaceRoot}/tests/org.palladiosimulator.supporting.problog.ui.tests"
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "UTF-8"
				lineDelimiter = "\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "org.palladiosimulator.supporting.problog.Problog"
			fileExtensions = "pl"
			referencedResource = "platform:/resource/org.palladiosimulator.supporting.problog.model/model/problog.genmodel"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateXtendStub = true
			}

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
}
